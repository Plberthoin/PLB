             PGM        PARM(&FROMSAVF &TOFILE &CRTFILE)
/* Paramètres */
             DCL        &FROMsavf *CHAR 20
             DCL        &TOFILE *CHAR 20
             DCL        &CRTFILE *LGL
/* */
             DCL        &Fsavf *CHAR 10
             DCL        &Flib  *CHAR 10
             DCL        &tfile *CHAR 10
             DCL        &tlib  *CHAR 10

             INCLUDE    SRCMBR(ERREUR1) SRCFILE(GCOMPILE/QCLSRC)
             MONMSG     MSGID(CPF0000) EXEC(GOTO ERREUR)
                CHGVAR     &Fsavf %SST(&FROMsavf 1 10)
                CHGVAR     &Flib  %SST(&FROMsavf 11 10)
                CHGVAR     &tfile %SST(&tofile   1 10)
                CHGVAR     &tlib  %SST(&tofile   11 10)
             OVRSAVF    FILE(COPYIN) TOFILE(&FLIB/&FSAVF) +
                          OVRSCOPE(*JOB)
             OVRDBF     FILE(COPYOUT) TOFILE(&TLIB/&TFILE) +
                          OVRSCOPE(*JOB)
/* traitement des paramètres */
if cond(&crtfile) then(do)
dltf FILE(&TLIB/&TFILE)
monmsg CPF2100
if cond(&tlib = '*LIBL') then(do)
chgvar &tlib '*CURLIB'
enddo
             CRTPF      FILE(&TLIB/&TFILE) RCDLEN(528) TEXT('Crée +
                          par CPYFRMSAVF') SIZE(*NOMAX)
enddo
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Copie en +
                          cours ...') TOPGMQ(*EXT) MSGTYPE(*STATUS)
             CALL        CPYSAVFR
             DLTOVR     FILE(COPYIN)
             monmsg cpf0000
             DLTOVR     FILE(COPYOUT)
             monmsg cpf0000
             SNDPGMMSG  MSG('Copie terminée') MSGTYPE(*COMP)
             INCLUDE    SRCMBR(ERREUR2) SRCFILE(GCOMPILE/QCLSRC)
ENDPGM
